class:: Disperser

summary:: Cascaded allpass filters

categories:: UGens>Filters

DESCRIPTION::

Cascaded Allpass Filters. 100% Twang.

CLASSMETHODS::

METHOD:: ar

ARGUMENT:: input
Audio input signal

ARGUMENT:: freq
Allpass filter cutoff frequency in Hz (20-20000)

ARGUMENT:: resonance
Filter resonance (0-1). Higher values create steeper phase responses at the cutoff frequency

ARGUMENT:: mix
Dry/wet crossfade (0-1). 0 = completely dry, 1 = completely wet

ARGUMENT:: feedback
Feedback amount (0-1). Higher values create more resonant peaks.

returns:: Audio rate UGen.

EXAMPLES::

subsection::1.1) Basic Disperser

code::
(
{
	var sig = Impulse.ar(2);
	Disperser.ar(
		input: sig,
		freq: 500,
		resonance: 0.5,
		mix: 1.0,
		feedback: 0.0
	)!2;
}.play;
)
::

subsection::1.2) Phaser with Feedback

code::
(
{
	var sig = Saw.ar(32.midicps);
	Disperser.ar(
		input: sig,
		freq: SinOsc.kr(0.3).linlin(-1, 1, 200, 4000),
		resonance: 0.5,
		mix: 0.5,
		feedback: 0.85,
	)!2 * 0.1
}.play;
)
::

subsection::1.3) Modulating the Steepness of the Phase Response (Resonance)

code::
(
{
	var sig = Saw.ar(32.midicps * (2 ** TIRand.ar(-1, 1, Dust.ar(2))));
	Disperser.ar(
		input: sig,
		freq: 500,
		resonance: SinOsc.kr(0.3).linlin(-1, 1, 0, 1),
		mix: 1.0,
		feedback: 0.85,
	)!2 * 0.1
}.play;
)
::